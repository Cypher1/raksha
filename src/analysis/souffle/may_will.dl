#ifndef MAY_WILL_DL_
#define MAY_WILL_DL_

#include "taint.dl"

.type Usage <: symbol

// this means `speaker` grants `actor` permission to do `usage` with `tag` 
// (where `tag` is owned by `speaker`)
.decl saysMay(speaker: Principal, actor: Principal, usage: Usage, tag: Tag)
// this means `speaker` would like to do `usage` with the data on `path`
.decl saysWill(speaker: Principal, usage: Usage, path: AccessPath)
// NOTE: for saysWill the speaker and actor could be separated, but it is 
// "safe" to always check for permissions when a usage request is made rather 
// than occasionally disbelieve that a request is being made.

// For each particle P, for each tag t in union over tags in read edges of P,
//      for each statement of the form:
//          `P says will <RELATION> using <HANDLE>`
//      prove
//          `ownsTag(Q, <TAG>)`
//          `Q says P may <RELATION> using <TAG>`

.decl permittedUsage(actor: Principal, usage: Usage, owner: Principal, tag: Tag)

permittedUsage(consumer, usage, owner, tag) :- ownsTag(owner, tag),
    saysMay(owner, consumer, usage, tag).

.decl disallowedUsage(dataConsumer: Principal, usage: Usage, owner: Principal, tag: Tag)

disallowedUsage(dataConsumer, usage, owner, tag) :-
    saysWill(dataConsumer, usage, path), mayHaveTag(path, owner, tag),
    !permittedUsage(dataConsumer, usage, owner, tag).

// This is a 0-ary relation that says all data usages are allowed. 
// Because souffle does not have 0-ary relations, it uses one dummy variable.
.decl allUsagesAllowed(dummy_var: symbol)

.decl isUsage(usage: Usage)

isPrincipal(speaker), isPrincipal(actor), isUsage(usage), isTag(tag) :-
  saysMay(speaker, actor, usage, tag).

isPrincipal(speaker), isUsage(usage), isAccessPath(ap) :- saysWill(speaker, usage, ap).

allUsagesAllowed("dummy_var") :- count : disallowedUsage(_, _, _, _) = 0.

// TODO(#270): Stop sending `may_will` into the `check` mechanisms.
// Catch may-will issues as if they were a check.
checkAndResult("may_will", "FAIL") :- !allUsagesAllowed("dummy_var").

// We phrase this as depending on saysMay so that tests that do not have "may"
// or "will" involved will not be considered as having had a check of this form
// at all. Again, we should find a better way to do this.
checkAndResult("may_will", "PASS") :- allUsagesAllowed("dummy_var").

#endif // MAY_WILL_DL_
